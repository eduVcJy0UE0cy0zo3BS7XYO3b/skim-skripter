(define-module (ren-sexp update)
  #:use-module (ice-9 match)
  #:use-module (goblins)
  #:use-module (dom window)
  #:use-module (ren-sexp text)
  #:use-module (ren-sexp bg)
  #:use-module (ren-sexp sprites)
  #:use-module (ren-sexp scene)
  #:use-module (ren-sexp carret)
  #:use-module (ren-sexp scene-utils)
  #:use-module (hoot ffi)
  #:use-module (fibers channels)
  #:use-module (ren-sexp draw)
  #:use-module (ren-sexp utils)
  #:export (init-update))

(define (inf-ttl? scene)
  (equal? (scene-ttl scene) 'inf))

(define (next-scene-increment next current)
  (pk (list next current))
  (match (list next current)
    ((($ <scene> state* bg* old-text*
	 text* sprites* music* carret* ttl*)
      ($ <scene> state  bg  old-text
	 text  sprites  music  carret  ttl))
     (cond
      ((not (same-bg? bg* bg))
       (next-bg next current))
      ((not (same-sprites? sprites* sprites))
       (next-sprites next current))
      ((not (same-old-text? old-text* old-text))
       (next-old-text next current))
      ((not (same-text? text* text))
       (next-string next current))
      ((and (number? ttl) (equal? ttl* (+ 1 ttl)))
       next)
      ((not (equal? ttl ttl*))
       (next-ttl next current))
      (else next)))))

(define (compute-next-state scene ^state)
  (let* ((next ($ ^state 'current-story-scene)))
    (if ($ ^state 'current-state-completed?)
	(if (inf-ttl? next)
	    scene
	    (append-empty-scene! ^state (make-scene)))
	(next-scene-increment next scene))))

(define (init-update ^state dt)
  (define draw-callback (init-draw ^state))
  (define (update)
    (let ((scene ($ ^state 'current-scene)))
      (match (scene-state scene)
	('play ($ ^state 'update-current-scene
		  (compute-next-state scene ^state)))
	(_ #t))
      (timeout update-callback dt)
      (request-animation-frame draw-callback)))
  (define update-callback (procedure->external update))
  update-callback)
